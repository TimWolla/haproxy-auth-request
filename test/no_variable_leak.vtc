varnishtest "Verify that we don't leak variables."
feature ignore_unknown_macro
feature cmd "dpkg --compare-versions ${haproxy_version} ge 2.2"

server s1 {
    rxreq
    txresp
} -start

server s_auth_backend {
    rxreq
    txresp \
        -status 301 \
        -hdr "location: https://example.com"
} -repeat 2 -start

haproxy h1 -conf {
    global
        lua-prepend-path ${testdir}/../?/http.lua
        lua-load ${testdir}/../auth-request.lua
        lua-load ${testdir}/no_variable_leak.lua

    listen fe1
        mode http
        bind "fd@${fe1}"

        http-request lua.auth-request auth_backend /allow
        http-response set-header leak-check %[lua.leak_check(txn.auth_response_location)]

        server s1 ${s1_addr}:${s1_port}

    backend auth_backend
        mode http
        server auth_backend ${s_auth_backend_addr}:${s_auth_backend_port}
} -start

client c1 -connect ${h1_fe1_sock} {
    txreq
    rxresp
    expect resp.http.leak-check == "<nil>"
} -run
